# Contributor: Daniel Leidisch <spam@leidisch.net>
# Contributor: Jens Staal <staal1978@gmail.com>
# Maintainer: aksr <aksr at t-com dot me>
pkgname="heirloom-sh"
pkgver=050706
pkgrel=5
pkgdesc="A portable variant of the traditional Unix shell, derived from OpenSolaris code."
arch=('i686' 'x86_64')
url="http://heirloom.sourceforge.net/sh.html"
license=('BSD' 'CDDL')
conflicts=('heirloom-sh-cvs')
source=("http://sourceforge.net/projects/heirloom/files/$pkgname/$pkgver/$pkgname-$pkgver.tar.bz2"
        '000-config.diff'
        '001-fix-gcc14-signal-handlers.patch'
        '002-fix-gcc14-test-c.patch')
sha256sums=('25fb8409e1eb75bb5da21ca32baf2d5eebcb8b84a1288d66e65763a125809e1d'
            'f0452b9fb1c58a6f0fba6d49bcd7598f875c94f64ddd8b60bb8ab12bbbaf7c9b'
            '7e586a915f5d753e77d6a35c5b0e1e0b20f0ef0873320373f0eef44f68027afe'
            '6fdab9db5cdbc99ff003c36e86ca3b5f1a31c5df3b5e3086c6a0db951e47f0a6')

prepare() {
  cd "$pkgname-$pkgver"
  patch -p1 < ../000-config.diff
  # Fix GCC 14 incompatible pointer type errors
  patch -p1 < ../001-fix-gcc14-signal-handlers.patch
  patch -p1 < ../002-fix-gcc14-test-c.patch
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make install ROOT="$pkgdir"
  install -Dm644 CALDERA.LICENSE "$pkgdir"/usr/share/licenses/"$pkgname"/CALDERA
}
